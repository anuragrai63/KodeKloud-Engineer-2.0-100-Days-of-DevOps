# Day 98: Launch EC2 in Private VPC Subnet Using Terraform

## Scenario

The Nautilus DevOps team is expanding their AWS infrastructure and requires the setup of a private Virtual Private Cloud (VPC) along with a subnet. This VPC and subnet configuration will ensure that resources deployed within them remain isolated from external networks and can only communicate within the VPC. Additionally, the team needs to provision an EC2 instance under the newly created private VPC. This instance should be accessible only from within the VPC, allowing for secure communication and resource management within the AWS environment.

- Create a VPC named `datacenter-priv-vpc` with the CIDR block `10.0.0.0/16`.
- Create a subnet named `datacenter-priv-subnet` inside the VPC with the CIDR block `10.0.1.0/24` and `auto-assign` IP option must not be `enabled`.
- Create an EC2 instance named `datacenter-priv-ec2` inside the subnet and instance type must be `t2.micro`.
- Ensure the security group of the EC2 instance allows access only from within the VPC's CIDR block.
- Create the `main.tf` file (do not create a separate `.tf` file) to provision the VPC, subnet and EC2 instance.
- Use `variables.tf` file with the following variable names:
  - `KKE_VPC_CIDR` for the VPC CIDR block.
  - `KKE_SUBNET_CIDR` for the subnet CIDR block.
- Use the `outputs.tf` file with the following variable names:
  - `KKE_vpc_name` for the name of the VPC.
  - `KKE_subnet_name` for the name of the subnet.
  - `KKE_ec2_private` for the name of the EC2 instance.

> **Note:** Right-click under the `EXPLORER` section in `VS Code` and select `Open in Integrated Terminal` to launch the terminal.

---

## Task

- Create `EC2 Instance`.

---

## Solution

### 1.  Provider.tf

```hcl 
terraform {
  required_providers {
    aws = {
      source = "hashicorp/aws"
      version = "5.91.0"
    }
  }
}

provider "aws" {
  region                      = "us-east-1"
  skip_credentials_validation = true
  skip_requesting_account_id  = true
  s3_use_path_style = true

endpoints {
    ec2            = "http://aws:4566"
    apigateway     = "http://aws:4566"
    cloudformation = "http://aws:4566"
    cloudwatch     = "http://aws:4566"
    dynamodb       = "http://aws:4566"
    es             = "http://aws:4566"
    firehose       = "http://aws:4566"
    iam            = "http://aws:4566"
    kinesis        = "http://aws:4566"
    lambda         = "http://aws:4566"
    route53        = "http://aws:4566"
    redshift       = "http://aws:4566"
    s3             = "http://aws:4566"
    secretsmanager = "http://aws:4566"
    ses            = "http://aws:4566"
    sns            = "http://aws:4566"
    sqs            = "http://aws:4566"
    ssm            = "http://aws:4566"
    stepfunctions  = "http://aws:4566"
    sts            = "http://aws:4566"
    rds            = "http://aws:4566"
  }
}


```

### 2.  Resource variables (variables.tf)

```hcl

variable "KKE_VPC_CIDR" {
  description = "CIDR block for the VPC"
  default     = "10.0.0.0/16"
}

variable "KKE_SUBNET_CIDR" {
  description = "CIDR block for the subnet"
  default     = "10.0.1.0/24"
}


```

---

### 3. Create Resource (main.tf)

```hcl


resource "aws_vpc" "datacenter_priv_vpc" {
  cidr_block = var.KKE_VPC_CIDR

  tags = {
    Name = "datacenter-priv-vpc"
  }
}


resource "aws_subnet" "datacenter_priv_subnet" {
  vpc_id                  = aws_vpc.datacenter_priv_vpc.id
  cidr_block              = var.KKE_SUBNET_CIDR
  map_public_ip_on_launch = false

  tags = {
    Name = "datacenter-priv-subnet"
  }
}


resource "aws_security_group" "datacenter_priv_sg" {
  name        = "datacenter-priv-sg"
  description = "Allow traffic only from within VPC CIDR"
  vpc_id      = aws_vpc.datacenter_priv_vpc.id

  ingress {
    from_port   = 0
    to_port     = 65535
    protocol    = "tcp"
    cidr_blocks = [var.KKE_VPC_CIDR]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = [var.KKE_VPC_CIDR]
  }

  tags = {
    Name = "datacenter-priv-sg"
  }
}


resource "aws_instance" "datacenter_priv_ec2" {
  ami           = "ami-0c101f26f147fa7fd" 
  instance_type = "t2.micro"
  subnet_id     = aws_subnet.datacenter_priv_subnet.id
  vpc_security_group_ids = [aws_security_group.datacenter_priv_sg.id]

  tags = {
    Name = "datacenter-priv-ec2"
  }
}

```

---


### 4. Resource output (outputs.tf)

```hcl

output "KKE_vpc_name" {
  value = aws_vpc.datacenter_priv_vpc.tags["Name"]
}

output "KKE_subnet_name" {
  value = aws_subnet.datacenter_priv_subnet.tags["Name"]
}

output "KKE_ec2_private" {
  value = aws_instance.datacenter_priv_ec2.tags["Name"]
}


```

---


### 5. Run Below

```bash
terraform init
terraform plan
terraform apply -auto-approve


```

---


## Result

> Check ouput :
```bash
terraform show
```
