# Day 99: Attach IAM Policy for DynamoDB Access Using Terraform

## Scenario

The DevOps team has been tasked with creating a secure DynamoDB table and enforcing fine-grained access control using IAM. This setup will allow secure and restricted access to the table from trusted AWS services only.
As a member of the Nautilus DevOps Team, your task is to perform the following using Terraform:

- Create a DynamoDB Table: Create a table named `datacenter-table` with minimal configuration.
- Create an IAM Role: Create an IAM role named `datacenter-role` that will be allowed to access the table.
- Create an IAM Policy: Create a policy named `datacenter-readonly-policy` that should grant read-only access (GetItem, Scan, Query) to the specific DynamoDB table and attach it to the role.
- Create the `main.tf` file (do not create a separate `.tf` file) to provision the table, role, and policy.
- Create the variables.tf file with the following variables:
  - `KKE_TABLE_NAME:` name of the DynamoDB table.
  - `KKE_ROLE_NAME:` name of the IAM role.
  - `KKE_POLICY_NAME:` name of the IAM policy
- Create the `outputs.tf` file with the following outputs:
  - `kke_dynamodb_table:` name of the DynamoDB table
  - `kke_iam_role_name:` name of the IAM role
  - `kke_iam_policy_name:` name of the IAM policy
- Define the actual values for these variables in the `terraform.tfvars` file.
- Ensure that the `IAM policy` allows only read access and restricts it to the specific `DynamoDB table` created.

> **Note:**
> The Terraform working directory is `/home/bob/terraform`.
> Right-click under the `EXPLORER` section in `VS Code` and select `Open in Integrated Terminal` to launch the terminal.
> Before submitting the task, ensure that `terraform plan` returns `No changes. Your infrastructure matches the configuration`.

---

## Task

- Create `IAM`.
- Create `DynamoDB`.

---

## Solution

### 1.  Provider.tf

```hcl 
terraform {
  required_providers {
    aws = {
      source = "hashicorp/aws"
      version = "5.91.0"
    }
  }
}

provider "aws" {
  region                      = "us-east-1"
  skip_credentials_validation = true
  skip_requesting_account_id  = true
  s3_use_path_style = true

endpoints {
    ec2            = "http://aws:4566"
    apigateway     = "http://aws:4566"
    cloudformation = "http://aws:4566"
    cloudwatch     = "http://aws:4566"
    dynamodb       = "http://aws:4566"
    es             = "http://aws:4566"
    firehose       = "http://aws:4566"
    iam            = "http://aws:4566"
    kinesis        = "http://aws:4566"
    lambda         = "http://aws:4566"
    route53        = "http://aws:4566"
    redshift       = "http://aws:4566"
    s3             = "http://aws:4566"
    secretsmanager = "http://aws:4566"
    ses            = "http://aws:4566"
    sns            = "http://aws:4566"
    sqs            = "http://aws:4566"
    ssm            = "http://aws:4566"
    stepfunctions  = "http://aws:4566"
    sts            = "http://aws:4566"
    rds            = "http://aws:4566"
  }
}


```

### 2.  Resource variables (variables.tf)

```hcl

variable "KKE_TABLE_NAME" {
  description = "Name of the DynamoDB table"
}

variable "KKE_ROLE_NAME" {
  description = "Name of the IAM role"
}

variable "KKE_POLICY_NAME" {
  description = "Name of the IAM policy"
}



```

---

### 3. Create Resource (main.tf)

```hcl

resource "aws_dynamodb_table" "datacenter_table" {
  name           = var.KKE_TABLE_NAME
  billing_mode   = "PAY_PER_REQUEST"
  hash_key       = "id"

  attribute {
    name = "id"
    type = "S"
  }

  tags = {
    Name = var.KKE_TABLE_NAME
  }
}


resource "aws_iam_role" "datacenter_role" {
  name = var.KKE_ROLE_NAME

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": {
        "Service": "dynamodb.amazonaws.com"
      },
      "Action": "sts:AssumeRole"
    }
  ]
}
EOF

  tags = {
    Name = var.KKE_ROLE_NAME
  }
}


resource "aws_iam_policy" "datacenter_readonly_policy" {
  name        = var.KKE_POLICY_NAME
  description = "Read-only access to specific DynamoDB table"

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": [
        "dynamodb:GetItem",
        "dynamodb:Scan",
        "dynamodb:Query"
      ],
      "Resource": "${aws_dynamodb_table.datacenter_table.arn}"
    }
  ]
}
EOF

  tags = {
    Name = var.KKE_POLICY_NAME
  }
}


resource "aws_iam_role_policy_attachment" "attach_policy" {
  role       = aws_iam_role.datacenter_role.name
  policy_arn = aws_iam_policy.datacenter_readonly_policy.arn
}


```

---


### 4. Resource output (outputs.tf)

```hcl

output "kke_dynamodb_table" {
  value = aws_dynamodb_table.datacenter_table.name
}

output "kke_iam_role_name" {
  value = aws_iam_role.datacenter_role.name
}

output "kke_iam_policy_name" {
  value = aws_iam_policy.datacenter_readonly_policy.name
}


```

---

### 4. Resource  (terraform.tfvars)

```hcl

KKE_TABLE_NAME  = "datacenter-table"
KKE_ROLE_NAME   = "datacenter-role"
KKE_POLICY_NAME = "datacenter-readonly-policy"

```
---

### 5. Run Below

```bash
terraform init
terraform plan
terraform apply -auto-approve


```

---


## Result

> Check ouput :
```bash
terraform show
```
